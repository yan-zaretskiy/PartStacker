set(wxBUILD_SHARED OFF)
if(CMAKE_CXX_STANDARD GREATER 20)
    set(wxBUILD_CXX_STANDARD 20)
else()
    set(wxBUILD_CXX_STANDARD ${CMAKE_CXX_STANDARD})
endif()
add_subdirectory(wxWidgets)

set(GLEW_USE_STATIC_LIBS ON)
add_subdirectory(glew/build/cmake)

if(WIN32)
    if(CMAKE_BUILD_TYPE STREQUAL "Debug")
        set(PARTSTACKER_COPY_WXWIDGETS_HEADERS_DIRECTORY "mswud")
    elseif(CMAKE_BUILD_TYPE MATCHES "Release")
        set(PARTSTACKER_COPY_WXWIDGETS_HEADERS_DIRECTORY "mswu")
    endif()
    add_custom_target(pstack_gui_copy_wx_headers
        COMMAND ${CMAKE_COMMAND} -E copy
            "${CMAKE_CURRENT_BINARY_DIR}/wxWidgets/lib/vc_x64_lib/${PARTSTACKER_COPY_WXWIDGETS_HEADERS_DIRECTORY}/wx/setup.h"
            "${PROJECT_SOURCE_DIR}/external/wxWidgets/lib/vc_x64_lib/${PARTSTACKER_COPY_WXWIDGETS_HEADERS_DIRECTORY}/wx/setup.h"
    )
endif()

# Functions from https://stackoverflow.com/a/62311397
function(get_all_targets var dir)
    set(targets)
    get_all_targets_recursive(targets ${dir})
    set(${var} ${targets} PARENT_SCOPE)
endfunction()
macro(get_all_targets_recursive targets dir)
    get_property(subdirectories DIRECTORY ${dir} PROPERTY SUBDIRECTORIES)
    foreach(subdir ${subdirectories})
        get_all_targets_recursive(${targets} ${subdir})
    endforeach()
    get_property(current_targets DIRECTORY ${dir} PROPERTY BUILDSYSTEM_TARGETS)
    list(APPEND ${targets} ${current_targets})
endmacro()

get_all_targets(external_targets "${CMAKE_CURRENT_LIST_DIR}")
foreach(target ${external_targets})
    get_target_property(folder ${target} FOLDER)
    if("${folder}" STREQUAL "CMakePredefinedTargets")
        continue()
    endif()
    if("${folder}" MATCHES "-NOTFOUND$")
        set(folder "external")
    else()
        set(folder "external/${folder}")
    endif()
    set_target_properties(${target} PROPERTIES FOLDER ${folder})
endforeach()
